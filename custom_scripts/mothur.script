#!/usr/bin/env mothur

#SBATCH -J AMPLICON
#SBATCH -o sbatch.mothur.log
#SBATCH -e sbatch.mothur.err
#SBATCH -p general -N 1 -c 8
#SBATCH --mem 32GB

# dir settings
set.dir(input=., output=mothur)
set.logfile(name=mothur/mothur.log)

#Make contigs from paired end sequences
make.contigs(file=mothur.input.list, processors=8)
summary.seqs(fasta=current)
screen.seqs(fasta=current, group=current,summary=current,maxambig=0, maxlength=275,minlength=225)
unique.seqs(fasta=current)
count.seqs(name=current,group=current)
summary.seqs(count=current)
align.seqs(fasta=current,reference=ref_db/silva.nr_v123.v4.align, flip=t)
summary.seqs(fasta=current,count=current)
screen.seqs(fasta=current,count=current,summary=current,start=8, end=9582, maxhomop=8)
summary.seqs(fasta=current, count=current)
filter.seqs(fasta=current, vertical=T,trump=.)
unique.seqs(fasta=current, count=current)
pre.cluster(fasta=current, count=current, diffs=2)
chimera.uchime(fasta=current,count=current, dereplicate=t)
remove.seqs(fasta=current,accnos=current)
classify.seqs(fasta=current, count=current, reference=ref_db/MiDAS_S123_2.1.3.fasta, taxonomy=ref_db/MiDAS_S123_2.1.3.mothur.tax, cutoff=80)
remove.lineage(fasta=current, count=current,taxonomy=current,taxon=Chloroplast-Mitochondria-unknown-Eukaryota)
cluster.split(fasta=current, count=current,taxonomy=current, splitmethod=classify, taxlevel=4, cutoff=0.03)
make.shared(list=current, count=current, label=0.03)
classify.otu(list=current, count=current, taxonomy=current, label=0.03)
count.groups(shared=current)
